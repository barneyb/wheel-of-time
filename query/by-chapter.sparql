PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix : <http://wot.barneyb.com/wot/>
prefix dcterms: <http://purl.org/dc/terms/>
SELECT ?chapter
?s ?p ?o ?pd ?dv ?pa ?av
#?s ?e ?p ?o
WHERE {
      :TheWheelTurns dcterms:isPartOf ?endBook ;
                     :chapterNumber ?endChapNum .
      ?endBook a :Book ;
               dcterms:date ?endPubDate .
      ?book a :Book ;
            dcterms:date ?pubDate .
      ?chapter a :Chapter ;
               dcterms:isPartOf ?book ;
               :chapterNumber ?chapNum .
      filter( ?pubDate < ?endPubDate || ( ?book = ?endBook && ?chapNum <= ?endChapNum ) )
      graph ?chapter {
                     BIND ( :RandAlThor as ?s )
                     # simple triples
                     { ?s ?p ?o .
                       FILTER ( !isBlank(?s) )
                       FILTER ( ?p != owl:topObjectProperty )
                     }
                     # reified statements
                     UNION {   [   a rdf:Statement ;
                                   rdf:subject ?s ;
                                   rdf:predicate ?p ;
                                   rdf:object ?o ;
                                   ?pd ?dv ;
                               ] .
                               FILTER ( ?pd NOT IN ( rdf:type
                                                   , rdf:subject, rdf:predicate, rdf:object
                                                   ) )
                           }
                     # see if there are any annotations
                     OPTIONAL {   [    a owl:Axiom ;
                                       owl:annotatedSource ?s ;
                                       owl:annotatedProperty ?p ;
                                       owl:annotatedTarget ?o ;
                                       ?pa ?av ;
                                  ] .
                                  FILTER ( ?pa NOT IN ( rdf:type
                                                      , owl:annotatedSource, owl:annotatedProperty, owl:annotatedTarget
                                                      ) )
                              }
#                     BIND ( :RandAlThor as ?s )
#                     ?e a :Event .
#                     ?e :hasParticipant ?s .
#                     ?e ?p ?o .
#                     FILTER ( ?p != rdf:type )
#                     FILTER ( ?o != ?s )
                     }
      }
order by ?pubDate ?chapNum
?s ?p ?o ?pd ?dv ?pa ?av
#?s ?e ?p ?o
