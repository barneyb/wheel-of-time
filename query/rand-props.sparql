PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX : <http://wot.barneyb.com/wot/>
SELECT
(COALESCE(?slbl, ?s) as ?sn)
(COALESCE(?plbl, ?p) as ?pn)
(COALESCE(?olbl, ?o) AS ?on)
(COALESCE(?pdlbl, ?pd) AS ?pdn)
(COALESCE(?dvlbl, ?dv) AS ?dvn)
(COALESCE(?palbl, ?pa) AS ?pan)
(COALESCE(?avlbl, ?av) AS ?avn)
WHERE {
      BIND ( :RandAlThor as ?s )
      # simple triples
      { ?s ?p ?o .
        FILTER ( !isBlank(?s) )
        FILTER ( ?p != owl:topObjectProperty )
      }
      # reified statements
      UNION {   [   a rdf:Statement ;
                    rdf:subject ?s ;
                    rdf:predicate ?p ;
                    rdf:object ?o ;
                    ?pd ?dv ;
                ] .
                FILTER ( ?pd NOT IN ( rdf:type
                                    , rdf:subject, rdf:predicate, rdf:object
                                    ) )
                OPTIONAL { ?dv rdfs:label ?dvlbl }
                OPTIONAL { ?pd rdfs:label ?pdlbl }
            }
      OPTIONAL { ?s rdfs:label ?slbl }
      OPTIONAL { ?o rdfs:label ?olbl }
      OPTIONAL { ?p rdfs:label ?plbl }
      # see if there are any annotations
      OPTIONAL {   [    a owl:Axiom ;
                        owl:annotatedSource ?s ;
                        owl:annotatedProperty ?p ;
                        owl:annotatedTarget ?o ;
                        ?pa ?av ;
                   ] .
                   FILTER ( ?pa NOT IN ( rdf:type
                                       , owl:annotatedSource, owl:annotatedProperty, owl:annotatedTarget
                                       ) )
                   OPTIONAL { ?av rdfs:label ?avlbl }
                   OPTIONAL { ?pa rdfs:label ?palbl }
               }
      }
ORDER BY ?pn ?on ?pdn ?dvn ?pan ?avn
